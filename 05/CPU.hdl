// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    ALU(x=DRegOut, y=AorMOut, 
    zx=instruction[11], nx=instruction[10], zy=instruction[9], 
    ny=instruction[8], f=instruction[7], no=instruction[6], 
    out=outM, out=feedback, zr=StatusZr, ng=StatusN);

    //レジスタ
    DRegister(in=feedback, load=DLoad, out=DRegOut);
    And(a=instruction[4], b=instruction[15], out=DLoad); //i=1かつd2=1
    ARegister(in=ARegIn, load=ALoad, out=ARegOut, out[0..14]=addressM);
    Not(in=instruction[15], out=notACommamd);
    Or(a=notACommamd, b=instruction[5], out=ALoad); //Aはi=0かd1=1の時に書き込み

    //writeMについて
    And(a=instruction[3], b=instruction[15], out=writeM);

    //Aにどの値を入れるか、AかMのどちらの値を選ぶか
    Mux16(a=instruction, b=feedback, sel=instruction[15], out=ARegIn);
    Mux16(a=ARegOut, b=inM, sel=instruction[12], out=AorMOut);

    //jump条件, ALUのステータスビットとjumpビットのうちいずれか一致
    Xor(a=StatusZr, b=StatusN, out=notStatusP);
    Not(in=notStatusP, out=StatusP);
    And(a=instruction[0], b=StatusP,  out=JumpStrict1);
    And(a=instruction[1], b=StatusZr, out=JumpStrict2);
    And(a=instruction[2], b=StatusN,  out=JumpStrict3);
    Or(a=JumpStrict1, b=JumpStrict2, out=tmpStrict);
    Or(a=JumpStrict3, b=tmpStrict, out=JumpStrict0);
    //A命令の時は無条件でPC＋1
    And(a=JumpStrict0, b=instruction[15], out=JumpStrict);

    PC(in=ARegOut, inc=true, load=JumpStrict, reset=reset, out[0..14]=pc);
}